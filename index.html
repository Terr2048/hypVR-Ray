<!DOCTYPE html>
<html lang="en">
<head>
  <!-- Basic Page Needs
  –––––––––––––––––––––––––––––––––––––––––––––––––– -->
  <meta charset="utf-8">
  <title>Hyperbolic Raymarching</title>
  <!-- Metas
  –––––––––––––––––––––––––––––––––––––––––––––––––– -->
  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
  <!-- Style
  –––––––––––––––––––––––––––––––––––––––––––––––––– -->
  <style>
      body {
          background-color: #000;
          color: #fff;
          margin: 0px;
          padding: 0;
          overflow: hidden;
      }
  </style>
  <!-- Favicon
  –––––––––––––––––––––––––––––––––––––––––––––––––– -->
  <link rel="icon" type="image/png" href="images/favicon.png">
</head>

<body>
  <img id="vr-icon" src="images/vr-icon.png" height="50px"
    style="
      position: fixed;
      bottom: 30px;
      right: 50px;
      display: none;
  "/>
</body>


<!-- Shaders
–––––––––––––––––––––––––––––––––––––––––––––––––– -->

<script type="x-shader/x-vertex" id="vertexShader">
  void main()
  {
      gl_Position = projectionMatrix * modelViewMatrix * vec4(position.xyz, 1.0);
  }
</script>

<script type="x-shader/x-fragment" id="fragmentShader">
  const int MAX_MARCHING_STEPS = 255;
  const float MIN_DIST = 0.0;
  const float MAX_DIST = 100.0;
  const float EPSILON = 0.0001;
  const float TILE_SIZE = 0.1;

  uniform vec2 screenResolution;
  uniform vec3 cameraOrigin;
  uniform vec3 cameraOffset;
  uniform vec4 cameraQuat;
  uniform float fov;


  float lorentzDot(vec4 u, vec4 v){
    return u.w*v.w - u.x*v.x - u.y*v.y - u.z*v.z;
  }

  vec4 projectToHyperboloid(vec4 v) {
    float scaleFactor = sqrt(lorentzDot(v,v));

    vec4 result;
    result.x = v.x / scaleFactor;
    result.y = v.y / scaleFactor;
    result.z = v.z / scaleFactor;
    result.w = v.w / scaleFactor;
    return result;
  }

  vec3 qtransform( vec4 q, vec3 v ){
    return v + 2.0*cross(cross(v, -q.xyz ) + q.w*v, -q.xyz);
  }

  vec4 qmult(vec4 q, vec4 r){
    float a = r.x*q.x - r.y*q.y - r.z*q.z - r.w*q.w;
    float b = r.x*q.y + r.y*q.x - r.z*q.w + r.w*q.z;
    float c = r.x*q.z + r.y*q.w + r.z*q.x - r.w*q.y;
    float d = r.x*q.w - r.y*q.z - r.z*q.y + r.w*q.x;
    return vec4(a,b,c,d);
  }

  vec4 qinverse(vec4 q){
    float d = q.x*q.x+q.y*q.y+q.z*q.z+q.w*q.w;
    return vec4(-q.x/d, -q.y/d, -q.z/d, q.w/d);
  }

  //-------------------------------------------------------
  //Hyperbolic Math functions
  //-------------------------------------------------------
  float cosh(float rads){
    float eX = exp(rads);
    return (0.5 * (eX + 1.0/eX));
  }

  float sinh(float rads){
    float eX = exp(rads);
    return (0.5 * (eX - 1.0/eX));
  }

  float acosh(float rads){ //must be more than 1
    return log(rads + sqrt(rads*rads-1.0));
  }

  float hypNorm(vec4 v){
  	return sqrt(abs(lorentzDot(v,v)));
  }

  vec4 vFromVPrime(vec4 u, vec4 vPrime){
    vec4 v = vPrime - lorentzDot(u, vPrime)*u;
    return (1.0/hypNorm(v)*v);
  }

  float hypDistance(vec4 u, vec4 v){
    float bUV = lorentzDot(u,v);
    return acosh(bUV);
  }

  mat4 translateByVector(vec3 v) { // trickery from Jeff Weeks' Curved Spaces app
    float dx = v.x;
    float dy = v.y;
    float dz = v.z;
    float len = sqrt(dx*dx + dy*dy + dz*dz);
    if (len == 0.0)
    {
      return mat4(1.0);
    }
    else
      {
        dx /= len;
        dy /= len;
        dz /= len;
        mat4 m = mat4(
          vec4(0, 0, 0, dx),
          vec4(0, 0, 0, dy),
          vec4(0, 0, 0, dz),
          vec4(dx,dy,dz, 0)
        );
        mat4 m2 = m*m;
        float c1 = sinh(len);
        float c2 = cosh(len) - 1.0;
        return mat4(1.0) + c1 * m + c2 * m2;
      }
  }

  //-------------------------------------------------------

  vec4 getRay(float fov, vec2 resolution, vec2 fragCoord){
    vec2 xy = 0.2*((fragCoord - 0.5*resolution)/resolution.x);
    float z = 0.1;
    vec3 pPre = qtransform(cameraQuat, vec3(xy,z));
    vec4 p = projectToHyperboloid(vec4(pPre, 1.0));
    return p;
  }

  //Raymarch functions http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
  //-------------------------------------------------------
  float unionSDF(float d1, float d2){
    return min(d1, d2);
  }

  float differenceSDF(float d1, float d2){
    return max(-d1, d2);
  }

  //Raymarch primitives

  float sphereHSDF(vec4 samplePoint, vec3 eTranslate){
    return hypDistance(vec4(0,0,0,1)*translateByVector(eTranslate), samplePoint) - 0.05;
  }

  float sceneHSDF(vec4 samplePoint){
    float final = unionSDF(sphereHSDF(samplePoint, vec3(0.1,0.0,0.5)), sphereHSDF(samplePoint, vec3(-0.1,0.0,0.5)));
    return final;
  }
  //-------------------------------------------------------
  /*bool isOutsideCell(vec4 rayOrigin){

  }
  vec4 fixOutsideCentralCell(vec4 rayOrigin, out vec4 tiling){

  }*/

  float shortestDistanceToSurface(vec4 rO, vec4 rD, float start, float end, out vec4 tiling){
    float depth = start;
    for(int i = 0; i< MAX_MARCHING_STEPS; i++){
      vec4 samplePoint = rO*cosh(depth)+rD*sinh(depth);
      float dist = sceneHSDF(samplePoint);
      if(dist < EPSILON){
        return depth;
      }
      depth += dist;
      if(depth >= end){
        return end;
      }
    }
    return end;
  }

 //COLORING FUNCTIONS ++++++++++++++++++++++++++++++++++++++++++++++++++++
 vec3 estimateNormal(vec4 p) {
   return normalize(vec3(
       sceneHSDF(p*translateByVector(vec3(EPSILON,0.0,0.0))) - sceneHSDF(p*translateByVector(vec3(-EPSILON,0.0,0.0))),
       sceneHSDF(p*translateByVector(vec3(0.0,EPSILON,0.0))) - sceneHSDF(p*translateByVector(vec3(0.0,-EPSILON,0.0))),
       sceneHSDF(p*translateByVector(vec3(0.0,0.0,EPSILON))) - sceneHSDF(p*translateByVector(vec3(0.0,0.0,-EPSILON)))
   ));
 }
 //+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  void main(){
    vec4 tiling;
    vec4 rayOrigin = projectToHyperboloid(vec4(0.0,0.0,0.0,1.0));
    rayOrigin *= translateByVector(cameraOffset);
    /*if(isOutsideCell(rayOrigin)){
      rayOrigin = fixOutsideCentralCell(rayOrigin, tiling);
    }*/
    vec4 rayDirVPrime = getRay(90.0, screenResolution, gl_FragCoord.xy);
    rayDirVPrime *= translateByVector(cameraOffset);
    vec4 rayDirV = vFromVPrime(rayOrigin, rayDirVPrime);

    float dist = shortestDistanceToSurface(rayOrigin, rayDirV, MIN_DIST, MAX_DIST, tiling);
    if(dist > MAX_DIST - EPSILON){
      //Didn't hit anything
      gl_FragColor = vec4(rayDirV.x,rayDirV.y,rayDirV.z,1.0);
      return;
    }
    //float depthColor = 1.0-dist/1.0;
    //gl_FragColor = vec4(depthColor,depthColor,depthColor,1.0);
    vec4 p = rayOrigin*cosh(dist)+rayDirV*sinh(dist);
    vec3 color = estimateNormal(p);
    gl_FragColor = vec4(color,1.0);
  }
</script>

<!-- Scripts
–––––––––––––––––––––––––––––––––––––––––––––––––– -->
<script src="js/lib/three.min.js"></script>
<script src="js/HypMath.js"></script>
<script src="js/vr/PhoneVR.js"></script>
<script src="js/vr/VRControlsHyperbolic.js"></script>
<script src="js/HyperSpace.js" id="mainCode"></script>
</html>
